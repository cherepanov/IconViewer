buildscript {
    ext {
        twelvemonkeysVersion = "3.6.2"
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij' // https://github.com/JetBrains/gradle-intellij-plugin
apply plugin: 'com.github.ben-manes.versions' // https://github.com/ben-manes/gradle-versions-plugin

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

group 'lermitage.intellij.iconviewer'
version version

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDir 'test'
    }
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    downloadSources Boolean.valueOf(downloadIdeaSources)
    instrumentCode Boolean.valueOf(instrumentPluginCode)
    pluginName 'Icon Viewer 2'
    sandboxDirectory = "${rootProject.projectDir}/.idea-sandbox/${ideaVersion}"
    updateSinceUntilBuild false
    version ideaVersion
}

runIde {
    jvmArgs '-Xms768m', '-Xmx2048m', '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
}

buildSearchableOptions {
    enabled true
    jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
}

repositories {
    mavenCentral()
}

// See https://github.com/ben-manes/gradle-versions-plugin
def versionIsNonStable = { String version ->
    if (['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().endsWith(qualifier) }) {
        return false
    }
    boolean isNonStable = ['alpha', 'Alpha', 'ALPHA', 'b', 'beta', 'Beta', 'BETA', 'rc', 'RC', 'M', 'EA', 'pr', 'atlassian'].any { qualifier ->
        version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
    }
    return isNonStable
}
dependencyUpdates.gradleReleaseChannel = "current"
dependencyUpdates {
    rejectVersionIf {
        versionIsNonStable(it.candidate.version)
    }
}

dependencies {
    implementation "com.twelvemonkeys.imageio:imageio-core:$twelvemonkeysVersion" // https://github.com/haraldk/TwelveMonkeys/releases
    implementation "com.twelvemonkeys.imageio:imageio-bmp:$twelvemonkeysVersion"  // https://github.com/haraldk/TwelveMonkeys/wiki/BMP-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-hdr:$twelvemonkeysVersion"  // https://github.com/haraldk/TwelveMonkeys/wiki/HDR-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-icns:$twelvemonkeysVersion" // https://github.com/haraldk/TwelveMonkeys/wiki/ICNS-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-iff:$twelvemonkeysVersion"  // https://github.com/haraldk/TwelveMonkeys/wiki/IFF-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-jpeg:$twelvemonkeysVersion" // https://github.com/haraldk/TwelveMonkeys/wiki/JPEG-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-pcx:$twelvemonkeysVersion"  // https://github.com/haraldk/TwelveMonkeys/wiki/PCX-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-pict:$twelvemonkeysVersion" // https://github.com/haraldk/TwelveMonkeys/wiki/PICT-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-pnm:$twelvemonkeysVersion"  // https://github.com/haraldk/TwelveMonkeys/wiki/PNM-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-psd:$twelvemonkeysVersion"  // https://github.com/haraldk/TwelveMonkeys/wiki/PSD-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-sgi:$twelvemonkeysVersion"  // https://github.com/haraldk/TwelveMonkeys/wiki/SGI-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-tga:$twelvemonkeysVersion"  // https://github.com/haraldk/TwelveMonkeys/wiki/TGA-Plugin
    implementation "com.twelvemonkeys.imageio:imageio-tiff:$twelvemonkeysVersion" // https://github.com/haraldk/TwelveMonkeys/wiki/TIFF-Plugin
    testImplementation "junit:junit:4.13.1"
}
